doc = {
    SOI ~ (PartName | Section | ApplyAll | Sentences | Selector | non_escaped_string)* ~ EOI
}

WHITESPACE = _{ " " | "\t" }

string  = @{ char+ }
escaped = _{ ("]" | "\\" | "}") }
char    =  {
    !(escaped) ~ ANY
  | "\\" ~ (escaped | "/" | "n" | "r" | "t")
}

non_escaped_string = @{ non_escaped_char+ }
non_escaped_char   =  {
    !("#" | "\\") ~ ANY
  | "\\" ~ ("#" | "\\" | "/" | "n" | "r" | "t")
}

Ident      = @{ ASCII_ALPHANUMERIC+ }
Ident_list =  { Ident ~ ("," ~ Ident)* }

PartName = { "#(" ~ Ident_list ~ ")" }

one_line_str = @{ (!NEWLINE ~ char)+ }
Section      =  { "#" ~ Ident? ~ "#"+ ~ one_line_str ~ (NEWLINE | EOI) }

All      = { "all" }
Idents   = { "[" ~ Ident_list ~ "]" }
ApplyAll = { "#" ~ Ident? ~ "{" ~ ((All | Idents) ~ ",")? ~ "{" ~ string ~ "}" ~ "}" }

Sen       = { "[" ~ string ~ "]" }
Sentences = { "#" ~ Ident? ~ Sen+ }

Selector = { "#." ~ "/"? ~ Ident? ~ ("." ~ Ident)* ~ "."? }
