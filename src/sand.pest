doc = {
    SOI ~ (PartName | Section | ApplyAll | Sentences | Selector | non_escaped_string)* ~ EOI
}

WHITESPACE = _{ " " | "\t" }

string  = @{ char+ }
escaped = _{ ("]" | "\\" | "}") }
char    =  {
    "\\" ~ (escaped | "/" | "n")
  | !(escaped) ~ ANY
}

non_escaped_string = @{ non_escaped_char+ }
non_escaped_char   =  {
    "\\" ~ ("#" | "\\" | "/" | "n")
  | !("#" | "\\") ~ ANY
}

Ident      = @{ (ASCII_ALPHANUMERIC | "_")+ }
Ident_list =  { Ident ~ ("," ~ Ident)* }

PartName = { "#(" ~ Ident_list ~ ")" }

one_line_str = @{ (!NEWLINE ~ char)+ }
hashes       = @{ "#"+ }
Section      =  { "#" ~ Ident? ~ hashes ~ one_line_str ~ (NEWLINE) }

All      = { "all" }
Idents   = { "[" ~ Ident_list ~ "]" }
ApplyAll = { "#" ~ Ident? ~ "{" ~ ((All | Idents) ~ ",")? ~ "{" ~ string ~ "}" ~ "}" }

Sen       = { "[" ~ string ~ "]" }
Sentences = { "#" ~ Ident? ~ Sen+ }

Slash    = { "/" }
LastDot  = { "." }
Selector = { "#." ~ Slash? ~ (Ident ~ ("." ~ Ident)* ~ LastDot?)? }
